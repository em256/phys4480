{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "b941e122-86f0-4a46-9836-c9a4abee9a7a",
   "metadata": {},
   "source": [
    "# Exploring Chaos with our Duffing Simulator"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "26b20ffd-7642-4a61-9f45-1f7e2c1290e0",
   "metadata": {},
   "source": [
    "Here is my version of the Duffing Simulator.  I added a few extra bells and whistles, but it is essentially the function you wrote in class.\n",
    "\n",
    "New elements:\n",
    "* I draw the potential surface\n",
    "* When you click, you produce not only an oscillator with the parameters corresponding to that point, but a whole ring of initial conditions around it.\n",
    "* I have sliders to control everything."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "21dc6bdb-2571-4dcb-bea0-80a442f60f6e",
   "metadata": {},
   "source": [
    "Note:  I put pretty much everything in my constructor as one monolithic block.  It makes more sense to do it like we did with the `pendulum`, and write functions that do each piece.  "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "6b4614df-477d-4d2f-8c75-c78e32f6cefb",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "startsim (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using GLMakie\n",
    "using DataStructures: CircularBuffer\n",
    "using LaTeXStrings\n",
    "\n",
    "mutable struct duffing\n",
    "    # Basic Scene info\n",
    "    fig # the Makie Figure object\n",
    "    ax # the Makie Axis object\n",
    "    # Time evolution info\n",
    "    xv # the phase space position of the oscillator\n",
    "    t # the current time\n",
    "    dt # the step size\n",
    "    step # our stepper -- we will use rk4step as the default\n",
    "    dxdt # the derivitive rule\n",
    "    p # parameters of the differential equation -- a struct\n",
    "      # with field F,ω,α,β,γ\n",
    "    # UI features\n",
    "    delay # how long to wait between frames\n",
    "    ball # the observable which will be plotted\n",
    "    traj # stores past points for drawing a \"tail\"\n",
    "    ptraj # not used\n",
    "    sliders # named tuple of UI elements to control force and frequency\n",
    "            # Can again use the same struct that we used for p,\n",
    "            # with attributes F,ω,α,β,γ\n",
    "    run # button to turn on and off animation\n",
    "    isrunning # is simulation running?\n",
    "    potential # stored values of the potential for plotting\n",
    "    grad # stored values of the gradient for plotting\n",
    "    bg\n",
    "    burst\n",
    "end\n",
    "\n",
    "mutable struct dp\n",
    "    F ::Float64\n",
    "    ω ::Float64\n",
    "    α ::Float64\n",
    "    β ::Float64\n",
    "    γ ::Float64\n",
    "end\n",
    "\n",
    "dp(;F,ω,α,β,γ)=dp(F,ω,α,β,γ)\n",
    "\n",
    "function rk4step(x,p,dxdt,t,deltat)\n",
    "    k1=dxdt(x,p,t)\n",
    "    k2=dxdt(x+k1*(deltat/2),p,t+deltat/2)\n",
    "    k3=dxdt(x+k2*(deltat/2),p,t+deltat/2)\n",
    "    k4=dxdt(x+k3*deltat,p,t+deltat)\n",
    "    return x+(k1+2*k2+2*k3+k4)*(deltat/6)\n",
    "end\n",
    "\n",
    "function duf_dxvdt(xv,p,t)\n",
    "    x,v = xv\n",
    "    return[v,-2*p.γ*v-p.α*x-p.β*x^3+p.F*cos(p.ω*t)]\n",
    "end\n",
    "\n",
    "function setxv!(d::duffing,xv)\n",
    "    d.xv=xv\n",
    "    d.ball[]=Point2f(xv...)\n",
    "    push!(d.traj[], Point2f(xv...))\n",
    "    notify(d.traj) # needed to tell GLMakie that we updated traj -- push! doesn't send that signal\n",
    "    d\n",
    "end\n",
    "\n",
    "function step!(d::duffing)\n",
    "    xv=d.step(d.xv,d.p,d.dxdt,d.t,d.dt)\n",
    "    setxv!(d,xv)\n",
    "    d.t+=d.dt\n",
    "    p=d.p\n",
    "    F=p.F\n",
    "    t=d.t\n",
    "    ω=p.ω\n",
    "    d.bg[]=d.potential-F*cos(ω*t).*d.grad\n",
    "    d.burst[]=[d.step(w,d.p,d.dxdt,d.t,d.dt) for w in d.burst[]]\n",
    "    d\n",
    "end\n",
    "\n",
    "function duffing(;\n",
    "        xv=[0.5,0.],\n",
    "        p=dp(α=-1,β=1,γ=0.1,F=0.,ω=1.),\n",
    "        t=0.,\n",
    "        dt=0.02,\n",
    "        delay=0.00001,\n",
    "        step=rk4step,\n",
    "        dxdt=duf_dxvdt,tail=300)\n",
    "    # Create Figure object\n",
    "    # \n",
    "    fig = Figure(); display(fig)\n",
    "    ax = Axis(fig[1,1])\n",
    "    ax.title = \"Duffing\"\n",
    "    ax.aspect = DataAspect() \n",
    "    # create background\n",
    "    xlist=range(-2,2,100)\n",
    "    ylist=range(-2,2,100)\n",
    "    potential=[p.α*x^2/2+p.β*x^4/4+y^2/2 for x in xlist, y in ylist];\n",
    "    grad=[x for x in xlist, y in ylist];\n",
    "    bg=Observable(potential+0. .* grad);\n",
    "    image!(ax,(-2,2),(-2,2),bg)\n",
    "    contour!(ax,(-2,2),(-2,2),bg,levels=20)\n",
    "    #\n",
    "    # Create ball object, and add it to scene\n",
    "    #\n",
    "    ball=Observable(Point2f(xv...))\n",
    "    scatter!(ax, ball; marker = :circle, strokewidth = 2, \n",
    "            strokecolor = :purple,\n",
    "            color = :black, markersize = 8)\n",
    "    # run button\n",
    "    # run button\n",
    "    isrunning = Observable(false)\n",
    "    label = map(cond -> cond ? \"Stop\" : \"Run\", isrunning)\n",
    "    run = Button(fig[2,1]; label = label, tellwidth = false)\n",
    "    on(run.clicks) do clicks; isrunning[] = !isrunning[]; end\n",
    "    # Make Sliders\n",
    "    Label(fig[1,2][1,1],L\"\\frac{dv}{dt}=-2\\gamma v-\\alpha x-\\beta x^3 +F \\cos(\\omega t).\")\n",
    "    sg=SliderGrid(fig[1,2][2,1],\n",
    "    (label=\"F\",range=0.:0.01:1.,format = \"{:.2f}\",startvalue=p.F),\n",
    "    (label=\"ω\",range=0.01:0.01:2,format = \"{:.2f}\",startvalue=p.ω),\n",
    "    (label=\"γ\",range=0.0:0.01:1,format = \"{:.2f}\",startvalue=p.γ),\n",
    "    width=200,tellheight=false)\n",
    "    Fobserver=sg.sliders[1].value;\n",
    "    ωobserver=sg.sliders[2].value;\n",
    "    γobserver=sg.sliders[3].value;\n",
    "    on(Fobserver) do Fobserver\n",
    "        p.F=Fobserver[]\n",
    "    end\n",
    "    on(ωobserver) do ωobserver\n",
    "        p.ω=ωobserver[]\n",
    "    end\n",
    "    on(γobserver) do γobserver\n",
    "        p.γ=γobserver[]\n",
    "    end\n",
    "    sg2=SliderGrid(fig[1,2][3,1],\n",
    "    (label=\"α\",range=-2.:0.01:2.,format = \"{:.2f}\",startvalue=p.α),\n",
    "    (label=\"β\",range=0.:0.01:1.,format = \"{:.2f}\",startvalue=p.β),\n",
    "    width=200,tellheight=false)\n",
    "    αobserver=sg2.sliders[1].value;\n",
    "    βobserver=sg2.sliders[2].value;\n",
    "    sg3=SliderGrid(fig[2,2],\n",
    "    (label=\"dt\",range=0.:0.002:0.2,format = \"{:.2f}\",startvalue=dt),\n",
    "    width=200,tellheight=false)\n",
    "    dtobserver=sg3.sliders[1].value;\n",
    "    # code moved below instantiation\n",
    "    # Now lets make the trail\n",
    "    traj = CircularBuffer{Point2f}(tail)\n",
    "    fill!(traj, Point2f(xv...))\n",
    "    traj = Observable(traj)\n",
    "    c = to_color(:purple)\n",
    "    tailcol = [RGBAf(c.r, c.g, c.b, (i/tail)^2) for i in 1:tail]\n",
    "    lines!(ax, traj; linewidth = 3, color = tailcol)\n",
    "    #Burst\n",
    "    burst=Observable([Point2f(xv[1]+0.1*cos(θ),xv[2]+0.1*sin(θ)) for θ in 0.:2*pi/1000:2*pi])\n",
    "    scatter!(ax,burst)\n",
    "    #Create object\n",
    "    d= duffing(\n",
    "        fig #= the Makie Figure object that you created=#,\n",
    "        ax #= the Makie Axis object that you created=#,\n",
    "        xv #= initial conditions passed from constructor=#,\n",
    "        t #= the current time passed from constructor=#,\n",
    "        dt #= the step size passed from constructor=#,\n",
    "        step #= our stepper passed from constructor=#,\n",
    "        dxdt #= the derivitive rule passed from constructor=#,\n",
    "        p #= passed from constructor=#,\n",
    "        delay #= passed from constructor=#,\n",
    "        ball #= the observable that you created =#,\n",
    "        traj #=traj=#,\n",
    "        [] #=ptraj -- empty list =#,\n",
    "        sg #=sliders  =#,\n",
    "        run #=run  =#,\n",
    "        isrunning #=isrunning=#,\n",
    "        potential #=potential=#,\n",
    "        grad #=gradient=#,\n",
    "        bg,burst)\n",
    "    on(dtobserver) do dtobserver\n",
    "        d.dt=dtobserver[]\n",
    "    end\n",
    "    on(αobserver) do αobserver\n",
    "        p.α=αobserver[]\n",
    "        xlist=range(-2,2,100)\n",
    "        ylist=range(-2,2,100)\n",
    "        d.potential=[p.α*x^2/2+p.β*x^4/4+y^2/2 for x in xlist, y in ylist];\n",
    "    end\n",
    "    on(βobserver) do βobserver\n",
    "        p.β=βobserver[]\n",
    "        xlist=range(-2,2,100)\n",
    "        ylist=range(-2,2,100)\n",
    "        d.potential=[p.α*x^2/2+p.β*x^4/4+y^2/2 for x in xlist, y in ylist];\n",
    "    end\n",
    "    # Set up interactive\n",
    "    Makie.deactivate_interaction!(ax, :rectanglezoom)\n",
    "    spoint = select_point(ax.scene, marker = :circle)\n",
    "    on(spoint) do z\n",
    "        setxv!(d,z)\n",
    "        burst[]=[Point2f(z[1]+0.1*cos(θ),z[2]+0.1*sin(θ)) for θ in 0.:2*pi/1000:2*pi]\n",
    "    end\n",
    "    startsim(d)\n",
    "    xlims!(ax,-2,2)\n",
    "    ylims!(ax,-2,2)\n",
    "    #\n",
    "    return d\n",
    "end\n",
    "\n",
    "function startsim(p::duffing)\n",
    "    isrunning_notifier = Condition()\n",
    "    on(cond -> cond && notify(isrunning_notifier), p.isrunning)\n",
    "        errormonitor(@async while true\n",
    "        if p.isrunning[]\n",
    "            isopen(p.fig.scene) || break # stops if window is closed\n",
    "            step!(p)\n",
    "            sleep(p.delay) \n",
    "        else\n",
    "            wait(isrunning_notifier)\n",
    "        end\n",
    "    end)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "16efc5fe-618c-407c-ae87-f683b22c4d97",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "duffing(Scene (600px, 450px):\n",
       "  0 Plots\n",
       "  6 Child Scenes:\n",
       "    ├ Scene (600px, 450px)\n",
       "    ├ Scene (600px, 450px)\n",
       "    ├ Scene (600px, 450px)\n",
       "    ├ Scene (600px, 450px)\n",
       "    ├ Scene (600px, 450px)\n",
       "    └ Scene (600px, 450px), Axis (6 plots), [0.5, 0.0], 0.0, 0.02, rk4step, duf_dxvdt, dp(0.0, 1.0, -1.0, 1.0, 0.1), 1.0e-5, Observable(Float32[0.5, 0.0]), Observable(Point{2, Float32}[[0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0]  …  [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0], [0.5, 0.0]]), Any[], SliderGrid(), Button(), Observable(false), [4.0 3.9200081624324046 … 3.9200081624324046 4.0; 3.7664231813746545 3.6864313438070595 … 3.6864313438070595 3.7664231813746545; … ; 3.7664231813746545 3.6864313438070595 … 3.6864313438070595 3.7664231813746545; 4.0 3.9200081624324046 … 3.9200081624324046 4.0], [-2.0 -2.0 … -2.0 -2.0; -1.9595959595959596 -1.9595959595959596 … -1.9595959595959596 -1.9595959595959596; … ; 1.9595959595959596 1.9595959595959596 … 1.9595959595959596 1.9595959595959596; 2.0 2.0 … 2.0 2.0], Observable([4.0 3.9200081624324046 … 3.9200081624324046 4.0; 3.7664231813746545 3.6864313438070595 … 3.6864313438070595 3.7664231813746545; … ; 3.7664231813746545 3.6864313438070595 … 3.6864313438070595 3.7664231813746545; 4.0 3.9200081624324046 … 3.9200081624324046 4.0]), Observable(Point{2, Float32}[[0.6, 0.0], [0.599998, 0.0006283144], [0.5999921, 0.001256604], [0.59998226, 0.0018848439], [0.59996843, 0.0025130096], [0.5999507, 0.0031410758], [0.599929, 0.0037690182], [0.5999033, 0.004396812], [0.5998737, 0.0050244317], [0.59984016, 0.0056518535]  …  [0.59984016, -0.0056518535], [0.5998737, -0.0050244317], [0.5999033, -0.004396812], [0.599929, -0.0037690182], [0.5999507, -0.0031410758], [0.59996843, -0.0025130096], [0.59998226, -0.0018848439], [0.5999921, -0.001256604], [0.599998, -0.0006283144], [0.6, -2.4492935f-17]]))"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "d1=duffing()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7cb61174-28e4-4e23-a136-825dd85cf733",
   "metadata": {},
   "source": [
    "<h2 style=\"border: 2px solid #000; border-radius: 10px; padding: 20px; background-color:red; color:white;\" >\n",
    "## In-Class Activity 1\n",
    "</h2>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d4f1c36f-52c5-4ff3-af9d-be51b0aebbb1",
   "metadata": {},
   "source": [
    "Everyone in class should start this running.  Get in groups of 2 and play with the following.  I'll have one or two groups explain what they saw afterwards.\n",
    "\n",
    "1. Set $\\alpha=1,\\beta=0,F=0,\\gamma=0$.  This is the simple harmonic oscillator.  How does it behave?\n",
    "2. Turn on $\\gamma$, how does that change the behavior?\n",
    "3. Now turn on $F$.  You can increase/decrease the timestep to speed up or slow down the motion.  Can you see resonant behavior?  What happens when $\\omega$ is very small?  Very big?"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f71024a3-4aa8-4d5a-9518-0a75a580dd57",
   "metadata": {},
   "source": [
    "<h2 style=\"border: 2px solid #000; border-radius: 10px; padding: 20px; background-color:red; color:white;\" >\n",
    "## In-Class Activity 2\n",
    "</h2>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9bc78457-217a-4557-875a-17305152a3c9",
   "metadata": {},
   "source": [
    "1. Now set $\\alpha=-1,\\beta=1,F=0,\\omega=1, \\gamma=0$.  There are 3 types of trajectories:  Those that circle $(x,v)=(1,0)$, those that circle $(x,v)=(0,1)$, and those that circle both points.\n",
    "2. Turn on $\\gamma$.  What happens to each of those 3 types of trajectories?  Click on different starting points, and see what the \"basins of attraction\" are for the various fixed points.  You can change `dt` to speed up or slow down the smulation.\n",
    "3. Now turn on a weak driving.  Try $F=0.1,\\gamma=0.07$.  Up the timestep so that the transients decay quickly.  How many different stable orbits can you find?  I found 4.\n",
    "4. Increase $F$ to $0.2$.  The transients should start to get interesting.  Run with a fast time-step to see if they die out.\n",
    "5. Now go to $F=0.29$.  You should get chaotic behavior here (at least for the right initial conditions).  What is happening with the blue points?  Can you come up with a story about what is happening with the phase space volume?  Does this shed light on the \"strange attractor\" that you saw in the Poincare section?"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "42bfc0f0-e87f-4f30-b5be-7969a2f26ca3",
   "metadata": {},
   "source": [
    "<h2 style=\"border: 2px solid #000; border-radius: 10px; padding: 20px; background-color:red; color:white;\" >\n",
    "## In-Class Activity 3\n",
    "</h2>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "94e9a149-e3bb-4a99-b53c-2f7e7e18ac70",
   "metadata": {},
   "source": [
    "If you set the parameters just right, one can find regular trajectories whose period is a multiple of the drive period.  The following gave me a trajectory whose period is twice the drive period: $F=0.32,\\omega=1.4,\\gamma=0.05,\\alpha=-1,\\beta=1$.  You might need to make a large timestep for a while to get rid of the transients.  You may also need to try a couple initial conditions.\n",
    "\n",
    "If I go to $F=0.34$ I get one whose period is 4 times the drive period.  To see it, up the timestep, so that the trails outline the whole multiple loop trajectory.\n",
    "\n",
    "This is the \"period doubling\" route to chaos.  As one approaches the chaotic point the periods get longer and longer, until they are infinite.\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "037d3bb5-e446-49c7-bbc4-62d7c68e608b",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
